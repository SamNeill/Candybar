apply plugin: 'com.android.application'
apply plugin: 'maven-publish'

android {
    signingConfigs {
        def keystoreFile = file('candybar.jks')
        if (keystoreFile.exists()) {
            release {
                keyAlias 'candybar'
                keyPassword 'candybar'
                storeFile keystoreFile
                storePassword 'candybar'

                v1SigningEnabled true
                v2SigningEnabled true
            }
        }
    }

    compileSdk rootProject.ext.CompileSdk

    namespace 'com.candybar.dev'

    defaultConfig {
        applicationId 'com.candybar.dev'
        minSdkVersion rootProject.ext.MinSdk
        targetSdkVersion rootProject.ext.TargetSdk
        versionCode 2
        versionName "2.0"

        // Naming app bundles
        setArchivesBaseName("${parent.name}-v${defaultConfig.versionName}")
    }

    buildTypes {
        release {
            def keystoreFile = file('candybar.jks')
            if (keystoreFile.exists()) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        buildConfig = true
    }

    lint {
        abortOnError false
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation project(':library')

    // TODO: Remove `//` below to enable OneSignal
    //implementation 'com.onesignal:OneSignal:[5.0.0, 5.99.99]'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.candybar'
            artifactId = 'candybar-dev'
            version = android.defaultConfig.versionName
            
            afterEvaluate {
                from components.getByName("release")
            }

            pom {
                name = 'CandyBar'
                description = 'CandyBar Dashboard - A Material Dashboard'
                url = 'https://github.com/SamNeill/Candybar'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'SamNeill'
                        name = 'Sam Neill'
                        email = ''
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/SamNeill/Candybar.git'
                    developerConnection = 'scm:git:ssh://github.com/SamNeill/Candybar.git'
                    url = 'https://github.com/SamNeill/Candybar'
                }
            }
        }
    }
    
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/${System.getenv("GITHUB_REPOSITORY")}")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}